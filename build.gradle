plugins {
  id "java"
  id "groovy"
  id 'java-library'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
  mavenCentral()
}

group = "com.hierynomus"
ext.moduleName = "${project.group}.${project.name}"

def bouncycastleVersion = "1.70"

dependencies {
  implementation "org.slf4j:slf4j-api:1.7.36"
  implementation "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
  implementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
  implementation "com.jcraft:jzlib:1.1.3"
  implementation "com.hierynomus:asn-one:0.6.0"
  implementation "net.i2p.crypto:eddsa:0.3.0"
}


if (!JavaVersion.current().isJava9Compatible()) {
  throw new GradleScriptException("Minimum compilation version is Java 9")
}

compileJava {
  options.compilerArgs.addAll(['--release', '7'])
}

tasks.withType(Jar) {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task fatJar(type: Jar) {
  manifest.from jar.manifest
  classifier = 'all'
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
  }
  with jar
}

artifacts {
  archives fatJar
}